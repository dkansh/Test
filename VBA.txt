Private Sub CommandButton1_Click()
    Dim filePath As String
    filePath = ChooseFile
    If filePath <> "" Then
        CreateTimesheet filePath
    End If
End Sub

Private Function ChooseFile() As String
    Dim fileExplorer As FileDialog
    Set fileExplorer = Application.FileDialog(msoFileDialogFilePicker)

    With fileExplorer
        'To allow or disable to multi select
        .AllowMultiSelect = False
        
        .Title = "Select Timesheet!"
        .Filters.Clear
        .Filters.Add "Excel file", "*.xlsx; *.xls"
        If .Show = -1 Then 'Any file is selected
            ChooseFile = .SelectedItems.Item(1)
        End If
    End With
End Function

Private Sub CreateTimesheet(filePath As String)
    Dim TSWorkbook As Workbook
    Dim TSWorksheet As Worksheet
    Dim InputWorkbook As Workbook
    Dim InputTimesheet As Worksheet
    Dim rowNum As Long
    Dim numberOfSheets As Integer
    
    Set TSWorkbook = Workbooks.Add
    Set TSWorksheet = TSWorkbook.Sheets("Sheet1")
    
    Set InputWorkbook = Workbooks.Open(filePath, True, True)
    
    Set InputTimesheet = InputWorkbook.Worksheets("Sheet1")
    rowNum = InputTimesheet.Cells(7, 1).EntireColumn.Find(What:="*", SearchOrder:=xlRows, SearchDirection:=xlPrevious, LookIn:=xlValues).Row
    
    InputTimesheet.Range("A7:A" & rowNum).Copy TSWorksheet.Range("A1")
    InputTimesheet.Range("B7:B" & rowNum).Copy TSWorksheet.Range("B1")
    
    numberOfSheets = InputWorkbook.Sheets.Count
    
    For counter = 1 To numberOfSheets
        Set InputTimesheet = InputWorkbook.Worksheets("Sheet" & counter)
        InputTimesheet.Range("C7:C" & rowNum).Copy TSWorksheet.Cells(1, counter + 2)
    Next
    
    InputWorkbook.Close False
    
    For rowCounter = 2 To (rowNum - 6)
        totalTime = 0
        For columnCounter = 3 To (numberOfSheets + 2)
            totalTime = totalTime + TimeValue(TSWorksheet.Cells(rowCounter, columnCounter).Value & ":00")
        Next
        TSWorksheet.Cells(rowCounter, numberOfSheets + 3).Value = Application.WorksheetFunction.Text(totalTime, "[h]:mm:ss")
    Next
    
End Sub
